//Check for duplicate log ingestion in the SecurityEvent table
//Added LogSource to identify if the logs are coming from different source such as a Windows Event Collector
//https://learn.microsoft.com/en-us/windows/win32/wec/windows-event-collector
SecurityEvent
| summarize Count = count() by Computer, TimeGenerated, EventData, _ResourceId
| where Count > 1
| distinct Computer, LogSource = extract("virtualmachines/([^/]+)", 1, _ResourceId)
| sort by Computer desc

//Check for duplicates in multiple tables for Windows endpoints
let timeRange = 1d;
let logs = union withsource=TableName
    SecurityEvent,
    Event
| where TimeGenerated > ago(timeRange);
logs
| summarize Count = count() by Computer, TimeGenerated, EventData, _ResourceId, TableName
| where Count > 1
| distinct Computer, LogSource = extract("virtualmachines/([^/]+)", 1, _ResourceId), TableName
