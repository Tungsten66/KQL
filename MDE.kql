// List all devices without a Platform update listed
// If a Server, Defender feature may not be installed (check with PowerShell Get-WindowsFeature -name Windows-Defender)
// If a client, Defender may be disabled by GPO (Navigate to HKLM\Software\Policies\Microsoft\Windows Defender and ensure DisableAntiSpyware is not present)
DeviceTvmInfoGathering
| extend AdditionalFields = parse_json(AdditionalFields)
| extend AvPlatformVersion = tostring(AdditionalFields.["AvPlatformVersion"])
| where OSPlatform startswith "Windows"
| where parse_version(AvPlatformVersion) == parse_version("0.0.0.0")

// List devices from DeviceInfo table that "Can be onboarded" and join with  DeviceNetworkInfo table to also get the IP address.
//This can be helpful to identify subnets/sites that may have network connectivity issues 
DeviceInfo
| where OnboardingStatus == "Can be onboarded"
| join kind=inner (
DeviceNetworkInfo
| extend IPList = todynamic(IPAddresses)
| mv-expand IPList
| extend IPAddress = tostring(IPList.IPAddress)
| project DeviceId, IPAddress
) on DeviceId
| summarize arg_max(Timestamp, *) by DeviceId
| project DeviceId, Timestamp, IPAddress, OnboardingStatus

//Limit search in DeviceEvents table to only machines that are in the "Windows Clients" Device Group
let DeviceGroup = DeviceInfo
| where MachineGroup == "Windows Clients"
| project DeviceId;
DeviceEvents
| where DeviceId in (DeviceGroup)
